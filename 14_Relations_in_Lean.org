#+Title: Logic and Proof
#+Author: [[http://www.andrew.cmu.edu/user/avigad][Jeremy Avigad]], [[http://www.andrew.cmu.edu/user/rlewis1/][Robert Y. Lewis]],  [[http://www.contrib.andrew.cmu.edu/~fpv/][Floris van Doorn]]

* Relations in Lean
:PROPERTIES:
  :CUSTOM_ID: Relations_in_Lean
:END:      

In first-order logic, a (binary) relation was defined to be a set of
pairs of elements of the domain. We adopted a similar definition in
informal mathematics, although we've tended to use infix notation for
relation symbols and have mostly ignored the "set-membership" definition.
In practice, a (binary) relation was a sort of function that took two
elements of the domain and returned either "true" or "false."

In Lean, we make this notion explicit: a binary relation on a type =A=
is a function =A → A → Prop=. If =R= has this type, and =a b : A=,
then =R a b= is a proposition.

As in informal mathematics, we often wish to use infix notation for our
relation. Lean allows us to do this. 

** Order Relations

We can reason about partial orders in Lean by fixing a type, =A=, and a
binary relation, =R=, and working under the hypotheses that =A= is
reflexive, transitive, and antisymmetric:
#+BEGIN_SRC lean
section
  parameters {A : Type} {R : A → A → Prop}
  hypothesis (reflR : reflexive R)
  hypothesis (transR : transitive R)
  hypothesis (antisymmR : ∀ {a b : A}, R a b → R b a → a = b)

  local infix ≤ := R
end
#+END_SRC
The =parameter= and =hypothesis= commands are similar to the
=variable= and =premise= commands, except that parameters are fixed
within a section. In other words, if you prove a theorem about =R= in
the section above, you cannot apply that theorem to another relation,
=S=, without closing the section. Since the parameter =R= is fixed,
Lean allows us to define notation for =R=, to be used locally in the
section. 

In the example below, having fixed a partial order, =R=, we define the
corresponding strict partial order and prove that it is, indeed, a
strict order.
#+BEGIN_SRC lean
open eq.ops

section
  parameters {A : Type} {R : A → A → Prop}
  hypothesis (reflR : reflexive R)
  hypothesis (transR : transitive R)
  hypothesis (antisymmR : ∀ {a b : A}, R a b → R b a → a = b)

  local infix ≤ := R

  definition R' (a b : A) : Prop := a ≤ b ∧ a ≠ b

  local infix < := R'

  theorem irrefl (a : A) : ¬ a < a :=
  suppose a < a,
  have a ≠ a, from and.right this,
  have a = a, from rfl,
  show false, from `a ≠ a` `a = a`

  theorem trans {a b c : A} (H₁ : a < b) (H₂ : b < c) : a < c :=
  have a ≤ b, from and.left H₁,
  have a ≠ b, from and.right H₁,
  have b ≤ c, from and.left H₂,
  have b ≠ c, from and.right H₂,
  have a ≤ c, from transR `a ≤ b` `b ≤ c`,
  have a ≠ c, from 
    suppose a = c,
      have c ≤ b, from `a = c` ▸ `a ≤ b`,
      have b = c, from antisymmR `b ≤ c` `c ≤ b`,
      show false, from `b ≠ c` `b = c`, 
  show a < c, from and.intro `a ≤ c` `a ≠ c`
end
#+END_SRC

Notice that we have used the command =open eq.ops= to avail ourselves
of the extra notation for equality proofs, so that the expression 
=`a = c` ▸ `a ≤ b`= denotes a proof of =c ≤ b=.

In the exercises, we ask you to show the other direction of this: 
from a strict partial order we can define a partial order.

** Orders on numbers

Conveniently, Lean has the normal orderings on the natural numbers,
integers, and so on defined already.

#+BEGIN_SRC lean 
open nat
variables n m : ℕ

check 0 ≤ n
check n < n + 1

example : 0 ≤ n := zero_le n
example : n < n + 1 := self_lt_succ n

example (H : n + 1 ≤ m) : n < m + 1 :=
have H1 : n < n + 1, from self_lt_succ n,
have H2 : n < m, from lt_of_lt_of_le H1 H,
have H3 : m < m + 1, from self_lt_succ m,
show n < m + 1, from lt.trans H2 H3
#+END_SRC

There are many theorems in Lean that are useful for proving facts
about inequality relations. We list some common ones here.

1. =zero_le : ∀ a : A, 0 ≤ a=
2. =self_lt_succ : ∀ a : A, a < a + 1=
3. =le_succ : ∀ a : A, a ≤ a + 1=
4. =le.trans : ∀ a b c : A, a ≤ b → b ≤ c → a ≤ c=
5. =lt.trans : ∀ a b c : A, a < b → b < c → a < c=
6. =lt_of_lt_of_le : ∀ a b c : A, a < b → b ≤ c → a < c=
7. =lt_of_le_of_lt : ∀ a b c : A, a ≤ b → b < c → a < c=
8. =le_of_lt : ∀ a b : A, a < b → a ≤ b=

Exercises

1. Replace the =sorry= commands in the following proofs to show that
   we can create a partial order =R'= out of a strict partial order =R=.
 
#+BEGIN_SRC lean
open eq.ops

section
  parameters {A : Type} {R : A → A → Prop}
  hypothesis (irreflR : irreflexive R)
  hypothesis (transR : transitive R)
  
  local infix < := R
  
  definition R' (a b : A) : Prop := R a b ∨ a = b
  local infix ≤ := R'
  
  theorem reflR' (a : A) : a ≤ a := sorry
  theorem transR' {a b c : A} (H1 : a ≤ b) (H2 : b ≤ c): a ≤ c := sorry
  theorem antisymmR' {a b : A} (H1 : a ≤ b) (H2 : b ≤ a) : a = b := sorry 
end
#+END_SRC

2. Complete the following proof. Note: we write =(1 : ℕ)= instead of just
   =1= so that Lean does not confuse the natural number =1= with the 
   integer, rational, or so on.

#+BEGIN_SRC lean
open nat

example : (1 : ℕ) ≤ (4 : ℕ) :=
sorry
#+END_SRC

3. Only one of the following two theorems is provable. Figure out which
   one is true, and replace the =sorry= command with a complete proof.

#+BEGIN_SRC lean 
open eq.ops
section

  parameters {A : Type} {a b c : A} {R : A → A → Prop}
  hypothesis (Rab : R a b)
  hypothesis (Rbc : R b c)
  hypothesis (nRac : ¬ R a c)
  
  -- Prove one of the following two theorems:
  
  theorem R_is_strict_partial_order : irreflexive R ∧ transitive R :=
  sorry
  
  theorem R_is_not_strict_partial_order : ¬(irreflexive R ∧ transitive R) :=
  sorry

end
#+END_SRC
